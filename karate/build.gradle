plugins {
   id 'java'
   id 'idea'
   id 'com.github.lkishalmi.gatling' version '0.7.4'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    gatling
}

dependencies {
    testCompile('com.intuit.karate:karate-apache:0.8.0.1')
    testCompile('com.intuit.karate:karate-junit4:0.8.0.1')
    testCompile('net.masterthought:cucumber-reporting:4.2.1')
    gatling('com.intuit.karate:karate-gatling:0.8.0.1')
    gatling ('com.intuit.karate:karate-apache:0.8.0.1')
    gatling('com.intuit.karate:karate-junit4:0.8.0.1')
}


sourceSets {
    gatling {
        scala.srcDirs = ['src/gatling/scala']
        resources.srcDirs = ['src/gatling/resources']
    }
    
    test {
        java {
            srcDir file('src/test/java')
        }
        resources {
            srcDir file('src/test/resources')
        }
    }
}

test {
    // Pull cucumber options into the cucumber JVM
    systemProperty "cucumber.options", System.properties.getProperty("cucumber.options")
    // Pull karate options into the JVM
    systemProperty "karate.env", System.properties.getProperty("karate.env")
    // Ensure tests are always run
    outputs.upToDateWhen { false }
}

gatling {
    sourceRoot = 'src/gatling/scala'
    simulationsDir = 'simulations'
    simulations = ['ContractKarateGatlingSimulation']
}

// exclude java compilation. gatling has to run on java 8, sources are on java 9.
gradle.taskGraph.whenReady {
    taskGraph ->
        if (taskGraph.hasTask(gatlingRun)) {
            compileJava.enabled = false
            compileTestJava.enabled = false
            compileGatlingJava.enabled = false
        }
}

idea {
    module {
        testSourceDirs += project.sourceSets.gatling.scala.srcDirs
        testSourceDirs += project.sourceSets.gatling.resources.srcDirs
    }
}
gatlingRun{
    systemProperties.remove("java.endorsed.dirs")
}
